&ANALYZE-SUSPEND _VERSION-NUMBER UIB_v8r11
&ANALYZE-RESUME
&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _DEFINITIONS Procedure 
/*--------------------------------------------------------------------------
    File        : r-fact01.p
    Purpose     : Impresion de Fact/Boletas 

    Syntax      :

    Description :

    Author(s)   :
    Created     :
    Notes       :
  ------------------------------------------------------------------------*/
/*          This .W file was created with the Progress UIB.             */
/*----------------------------------------------------------------------*/

/* ***************************  Definitions  ************************** */
DEFINE INPUT PARAMETER X-ROWID AS ROWID.

DEF SHARED VAR s-codcia AS INT.
DEF SHARED VAR cl-codcia AS INT.
DEF SHARED VAR pv-codcia AS INT.
DEF SHARED VAR s-coddiv LIKE gn-divi.coddiv.
DEF SHARED VAR s-codter LIKE ccbcterm.codter.

DEF VAR X-EnLetras AS CHAR FORMAT "x(100)" NO-UNDO.
DEF VAR C-NomVen AS CHAR FORMAT "X(21)".
DEF VAR C-NomCon AS CHAR FORMAT "X(30)".
DEF VAR C-RucTra AS CHAR FORMAT "X(30)".
DEF VAR C-NomTra AS CHAR FORMAT "X(30)".
DEF VAR C-DirTra AS CHAR FORMAT "X(30)".
DEF VAR I-NroItm AS INTEGER.
DEF VAR S-TOTPES AS DECIMAL.
DEF VAR X-senal  AS CHAR.
DEF VAR X-ZONA   AS CHAR.
DEF VAR X-DEPART LIKE TabDepto.NomDepto.
DEF VAR X-LugPar LIKE Almacen.DirAlm.
DEF VAR X-FchRef LIKE ccbcdocu.fchdoc.
 
DEFINE BUFFER B-DOCU FOR ccbcdocu.

FIND ccbcdocu WHERE ROWID(ccbcdocu) = X-ROWID NO-LOCK NO-ERROR.
IF NOT AVAILABLE ccbcdocu THEN RETURN.

FIND B-DOCU WHERE B-DOCU.CODCIA = ccbcdocu.CodCia AND
                  B-DOCU.CodDoc = ccbcdocu.CodRef AND
                  B-DOCU.NroDoc = ccbcdocu.NroRef NO-LOCK NO-ERROR.
IF AVAILABLE B-DOCU THEN X-FchRef = B-DOCU.FchDoc.
ELSE X-FchRef = ?.

FIND gn-clie WHERE gn-clie.codcia = cl-codcia AND
    gn-clie.codcli = ccbcdocu.codcli NO-LOCK.
    
X-senal  = "X".
C-NomVen = CcbCDocu.CodVen.
C-NomCon = CcbCDocu.FmaPgo.
C-RucTra = CcbCDocu.CodAge.

FIND gn-ven WHERE gn-ven.CodCia = CcbCDocu.CodCia AND
     gn-ven.CodVen = CcbCDocu.CodVen NO-LOCK NO-ERROR.
IF AVAILABLE gn-ven THEN C-NomVen = gn-ven.NomVen.

FIND gn-ConVt WHERE gn-ConVt.Codig = CcbCDocu.FmaPgo NO-LOCK NO-ERROR.
IF AVAILABLE gn-ConVt THEN C-NomCon = C-NomCon + " - " + gn-ConVt.Nombr.

FIND admrutas WHERE admruta.CodPro = CcbCDocu.CodAge NO-LOCK NO-ERROR.
IF AVAILABLE admrutas THEN C-NomTra = admrutas.NomTra.

FIND gn-prov WHERE gn-prov.codcia = pv-codcia AND
    gn-prov.codpro = C-RucTra NO-LOCK.
IF AVAILABLE gn-prov THEN C-DirTra = gn-prov.DirPro.

FIND Almacen WHERE 
     Almacen.CodCia = CcbCDocu.CodCia AND
     Almacen.CodAlm = CcbCDocu.CodAlm NO-LOCK NO-ERROR.
IF AVAILABLE Almacen THEN X-LugPar = Almacen.DirAlm.

FIND TabDepto WHERE TabDepto.CodDepto = CcbCDocu.CodDpto NO-LOCK NO-ERROR.
IF AVAILABLE TabDepto THEN X-DEPART = TabDepto.NomDepto.

/* Definimos impresoras */
DEFINE VAR s-printer-list AS CHAR.
DEFINE VAR s-port-list AS CHAR.
DEFINE VAR s-port-name AS CHAR format "x(20)".
DEFINE VAR s-printer-count AS INTEGER.

RUN aderb/_prlist.p(
    OUTPUT s-printer-list,
    OUTPUT s-port-list,
    OUTPUT s-printer-count).

FIND FacCorre WHERE FacCorre.CodCia = S-CODCIA AND 
     FacCorre.CodDiv = S-CODDIV AND
     FacCorre.CodDoc = CcbCDocu.CodDoc AND
     FacCorre.NroSer = INTEGER(SUBSTRING(CcbCDocu.NroDoc,1,3))  NO-LOCK.

IF LOOKUP(FacCorre.Printer, s-printer-list) = 0 THEN DO:
   MESSAGE "Impresora " FacCorre.Printer " no esta instalada" VIEW-AS ALERT-BOX ERROR.
   RETURN ERROR.
END.

s-port-name = ENTRY(LOOKUP(FacCorre.Printer, s-printer-list), s-port-list).
s-port-name = REPLACE(S-PORT-NAME, ":", "").

/************************  PUNTEROS EN POSICION  *******************************/
RUN bin/_numero(ccbcdocu.imptot, 2, 1, OUTPUT X-EnLetras).
X-EnLetras = X-EnLetras + (IF ccbcdocu.codmon = 1 THEN " NUEVOS SOLES" ELSE " DOLARES AMERICANOS").

/************************  DEFINICION DE FRAMES  *******************************/
DEFINE FRAME F-HdrGui
    HEADER
    SKIP(9)
    gn-clie.nomcli  AT 13  FORMAT "x(40)" 
    ccbcdocu.fchdoc AT 105 
    CcbCDocu.CodDiv AT 132 SKIP
    CcbCDocu.LugEnt AT 13  FORMAT "x(60)" X-ZONA FORMAT "X(25)"
    CcbCDocu.NroPed AT 105 FORMAT "XXX-XXXXXX" SKIP
    gn-clie.ruc     AT 13 
    CcbCDocu.CodCli AT 40
    gn-clie.Telfnos[1] AT 55
    CcbCDocu.NroOrd    AT 83 
    CcbCDocu.NroDoc AT 105 FORMAT "XXX-XXXXXX" SKIP 
    CcbcDocu.CodRef AT 13 CcbcDocu.NroRef  X-FchRef AT 30
    C-NomCon AT 53
    X-DEPART  AT 105 FORMAT "X(15)"
    CcbCDocu.CodVen AT 128
    X-LugPar AT 12 VIEW-AS TEXT FORMAT "X(80)"
    C-NomVen AT 116 SKIP(2) 
    WITH PAGE-TOP NO-LABELS NO-BOX STREAM-IO WIDTH 200.


DEFINE FRAME F-DetaGui
    I-NroItm AT 01 FORMAT "Z9"
    AlmDMov.codmat  AT 08 FORMAT "999999"
    almmmatg.desmat AT 20 FORMAT "x(50)"
    almmmatg.DesMar AT 79 FORMAT "x(10)"
    AlmDMov.candes  AT 94 FORMAT ">,>>9.99"
    AlmDMov.CodUnd  AT 104
    AlmDMov.preuni  AT 110 FORMAT ">>,>>9.9999"
    AlmDMov.implin  AT 125 FORMAT ">>>>,>>9.99" 
    WITH NO-LABELS NO-BOX NO-UNDERLINE STREAM-IO WIDTH 200.



DEFINE FRAME F-FtrGui
    HEADER
    SKIP(1)
    "SON : " X-EnLetras AT 10 SKIP(3)
    C-NomTra   AT 15    X-senal  AT 61
    IF ccbcdocu.codmon = 1 THEN "S/."  ELSE "US$." AT 122 FORMAT "XXXX"
    ccbcdocu.imptot AT 126 FORMAT ">,>>>,>>9.99" SKIP
    C-DirTra   AT 15
    C-RucTra   AT 15 SKIP(4)
    "TOTAL KILOS :" AT 30 S-TOTPES AT 44 FORMAT ">>,>>9.99" 
    "HORA : " TO 1 STRING(TIME,"HH:MM:SS")
    WITH PAGE-BOTTOM NO-LABELS NO-UNDERLINE NO-BOX STREAM-IO WIDTH 200.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


&ANALYZE-SUSPEND _UIB-PREPROCESSOR-BLOCK 

/* ********************  Preprocessor Definitions  ******************** */

&Scoped-define PROCEDURE-TYPE Procedure
&Scoped-define DB-AWARE no



/* _UIB-PREPROCESSOR-BLOCK-END */
&ANALYZE-RESUME



/* *********************** Procedure Settings ************************ */

&ANALYZE-SUSPEND _PROCEDURE-SETTINGS
/* Settings for THIS-PROCEDURE
   Type: Procedure
   Allow: 
   Frames: 0
   Add Fields to: Neither
   Other Settings: CODE-ONLY COMPILE
 */
&ANALYZE-RESUME _END-PROCEDURE-SETTINGS

/* *************************  Create Window  ************************** */

&ANALYZE-SUSPEND _CREATE-WINDOW
/* DESIGN Window definition (used by the UIB) 
  CREATE WINDOW Procedure ASSIGN
         HEIGHT             = 2.15
         WIDTH              = 34.14.
/* END WINDOW DEFINITION */
                                                                        */
&ANALYZE-RESUME

 


&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _MAIN-BLOCK Procedure 


/* ***************************  Main Block  *************************** */
OUTPUT TO VALUE(s-port-name) PAGE-SIZE 60.
PUT CONTROL CHR(27) "@".
PUT CONTROL CHR(27) "C" CHR(66).
PUT CONTROL CHR(27) CHR(120) 0.
PUT CONTROL CHR(15).
I-NroItm = 0.
FOR EACH Almdmov NO-LOCK WHERE Almdmov.CodCia = CcbCDocu.CodCia
     AND Almdmov.CodAlm = CcbCDocu.CodAlm
     AND Almdmov.TipMov = "S"
     AND Almdmov.CodMov = CcbCDocu.CodMov
     AND Almdmov.NroSer = INTEGER(SUBSTRING(CcbCDocu.NroDoc,1,3))
     AND Almdmov.NroDoc = INTEGER(SUBSTRING(CcbCDocu.NroDoc,4,6)),
     FIRST almmmatg OF Almdmov  NO-LOCK
      BREAK BY Almdmov.NroDoc 
        BY Almdmov.CodMat:
    I-NroItm = I-NroItm + 1.
    S-TOTPES = S-TOTPES + ( AlmDMov.candes * Almmmatg.Pesmat ).
    VIEW FRAME F-HdrGui.
    VIEW FRAME F-FtrGui.
    DISPLAY I-NroItm AlmDMov.codmat almmmatg.desmat almmmatg.DesMar 
            AlmDMov.candes Almdmov.CodUnd AlmDMov.preuni AlmDMov.implin 
            WITH FRAME F-DetaGui.
    IF LAST-OF(AlmDMov.nrodoc) THEN DO:
        PAGE.
    END.
END.
OUTPUT CLOSE.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


