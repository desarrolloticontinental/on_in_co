&ANALYZE-SUSPEND _VERSION-NUMBER UIB_v8r12
&ANALYZE-RESUME
&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _DEFINITIONS Procedure 
/*--------------------------------------------------------------------------
    File        : r-ImpPed.p
    Purpose     : Impresion de Pedidos y Cotizaciones
    Syntax      :
    Description :
    Author(s)   :
    Created     :
    Notes       :
  ------------------------------------------------------------------------*/
/*          This .W file was created with the Progress UIB.             */
/*----------------------------------------------------------------------*/
/* ***************************  Definitions  ************************** */
DEFINE INPUT PARAMETER X-ROWID AS ROWID.
DEFINE INPUT PARAMETER l-incigv AS LOGICAL.

DEF SHARED VAR S-USER-ID AS CHAR. 
DEF SHARED VAR s-codcia  AS INT.
DEF SHARED VAR cl-CODCIA AS INTEGER.
DEF SHARED VAR s-codalm  AS CHAR.
DEF SHARED VAR s-coddiv  LIKE gn-divi.coddiv.
DEF SHARED VAR s-codter  LIKE ccbcterm.codter.
DEF SHARED VAR S-NomCia  AS CHAR.
DEF        VAR C-NomVen  AS CHAR FORMAT "X(30)".
DEF        VAR C-Descli  AS CHAR FORMAT "X(60)".
DEF        VAR C-Moneda  AS CHAR FORMAT "X(7)".
DEF        VAR C-NomCon  AS CHAR FORMAT "X(50)".
DEF        VAR C-TitDoc  AS CHAR FORMAT "X(50)".
DEF        VAR XD        AS CHAR FORMAT "X(5)".
DEF        VAR I-NroItm  AS INTEGER.
DEF        VAR F-PreNet  AS DECIMAL.
DEF        VAR W-DIRALM  AS CHAR FORMAT "X(50)".
DEF        VAR W-TLFALM  AS CHAR FORMAT "X(65)".

DEF        VAR F-PreUni  LIKE FacDPedi.Preuni.
DEF        VAR F-ImpLin  LIKE FacDPedi.ImpLin.
DEF        VAR F-ImpTot  LIKE FacCPedi.ImpTot.
DEF        VAR F-PesMat  AS DEC NO-UNDO INIT 0.

DEF        VAR dPreUni   AS DEC NO-UNDO. 
DEF        VAR dImpLin   AS DEC NO-UNDO.
DEF        VAR dImpTot   AS DEC NO-UNDO.

DEFINE VARIABLE x-dscto AS DECIMAL.

DEFINE VARIABLE X-IMPIGV AS CHARACTER FORMAT "X(30)".
DEF VAR X-EnLetras AS CHAR FORMAT "x(100)" NO-UNDO.
DEF VAR X-Percepcion AS CHAR FORMAT "x(60)" NO-UNDO.

FIND FacCPedi WHERE ROWID(FacCPedi) = X-ROWID NO-LOCK NO-ERROR.

IF NOT AVAILABLE FacCPedi THEN RETURN.

IF FacCPedi.CodDoc = "PED" THEN C-TitDoc = "    PEDIDO :". 
ELSE C-TitDoc = "COTIZACION :". 

IF FacCpedi.Codmon = 2 THEN C-Moneda = "DOLARES US$.".
ELSE C-Moneda = "SOLES   S/. ".

ASSIGN 
    C-NomVen = FacCPedi.CodVen
    C-NomCon = FacCPedi.FmaPgo
    XD       = STRING (FacCpedi.Fchven - FacCpedi.Fchped,"99999").

IF l-incigv THEN 
    ASSIGN 
        X-IMPIGV = "LOS PRECIOS INCLUYEN EL I.G.V."
        F-ImpTot = FacCPedi.ImpTot.
ELSE ASSIGN 
        X-IMPIGV = "LOS PRECIOS NO INCLUYEN EL I.G.V."
        F-ImpTot = FacCPedi.ImpVta.

FIND gn-clie WHERE 
     gn-clie.codcia = cl-codcia AND  
     gn-clie.codcli = FacCPedi.codcli NO-LOCK NO-ERROR.
C-DESCLI  = FaccPedi.codcli + ' - ' + FaccPedi.Nomcli     .

FIND gn-ven WHERE 
     gn-ven.CodCia = FacCPedi.CodCia AND  
     gn-ven.CodVen = FacCPedi.CodVen 
     NO-LOCK NO-ERROR.
IF AVAILABLE gn-ven THEN C-NomVen = C-NomVen + " - " + gn-ven.NomVen.

FIND gn-ConVt WHERE gn-ConVt.Codig = FacCPedi.FmaPgo NO-LOCK NO-ERROR.
IF AVAILABLE gn-ConVt THEN C-NomCon = FacCPedi.FmaPgo + " " + gn-ConVt.Nombr.

FIND Almacen WHERE 
     Almacen.CodCia = S-CODCIA AND  
     Almacen.CodAlm = S-CODALM 
     NO-LOCK NO-ERROR.
IF AVAILABLE Almacen THEN 
    ASSIGN 
        W-DIRALM = Almacen.DirAlm
        W-TLFALM = Almacen.TelAlm
        W-TLFALM = 'Telemarketing:(511) 349-2351 / 349-2444  Fax:349-4670'.  

/************************  PUNTEROS EN POSICION  *******************************/
/*RUN bin/_numero(F-IMPTOT, 2, 1, OUTPUT X-EnLetras).*/
RUN lib/_numero(F-IMPTOT, 2, 1, OUTPUT X-EnLetras).
X-EnLetras = "SON : " + X-EnLetras + (IF FacCPedi.codmon = 1 THEN " SOLES" ELSE " DOLARES AMERICANOS").
x-Percepcion = "".
IF Faccpedi.acubon[5] > 0 THEN x-Percepcion = "* Operación sujeta a percepción del IGV: " +
    (IF FacCPedi.codmon = 1 THEN "S/." ELSE "US$") + TRIM(STRING(Faccpedi.acubon[5], '>>>,>>9.99')).

/*Define Observaciones*/
DEFINE VAR F-Observa  AS CHAR NO-UNDO.

/************************  DEFINICION DE FRAMES  *******************************/

f-observa = FacCPedi.Observa.
IF NOT l-incigv THEN f-observa = REPLACE(f-observa, "INCLUYEN", "NO INCLUYEN").

DEFINE VARIABLE C-OBS AS CHAR EXTENT 2.
DEFINE VARIABLE K AS INTEGER.
IF NUM-ENTRIES(f-observa,"-") - 1 > 6 THEN DO:
   DO K = 2 TO 7:
      IF ENTRY(K,f-observa,"-") <> "" THEN 
         C-OBS[1] = C-OBS[1] + "- " + ENTRY(K,f-observa,"-").
   END.
   DO K = 8 TO NUM-ENTRIES(f-observa,"-"):
      IF ENTRY(K,f-observa,"-") <> "" THEN 
         C-OBS[2] = C-OBS[2] + "- " + ENTRY(K,f-observa,"-").
   END.
END.
ELSE DO: 
   C-OBS[1] = f-observa.
   C-OBS[2] = "".
END.

/******************/

DEFINE FRAME F-DetaCot
    I-NroItm FORMAT ">>>9"                  COLUMN-LABEL "ITEM"
    FacDPedi.codmat FORMAT "X(6)"           COLUMN-LABEL "CODIGO"
    FacDPedi.CanPed FORMAT ">>>,>>9.99"     COLUMN-LABEL "CANTIDAD"
    FacDPedi.undvta FORMAT "X(4)"           COLUMN-LABEL "UND."
    almmmatg.desmat FORMAT "X(45)"          COLUMN-LABEL "D E S C R I P C I O N"        
    almmmatg.desmar FORMAT "X(10)"          COLUMN-LABEL "M A R C A"
    Facdpedi.PreUni FORMAT "->>,>>9.99999"  COLUMN-LABEL "PRE VTA"    
    FacDPedi.PorDto FORMAT "->>9.99 %"      COLUMN-LABEL "% DCTO"
    /*F-PreUni FORMAT "->>,>>9.9999"          COLUMN-LABEL "PRE UNI"*/
    dPreUni         FORMAT "->>,>>9.99999"  COLUMN-LABEL "PRE UNI"
    /*F-ImpLin FORMAT "->,>>>,>>9.9999"       COLUMN-LABEL "TOTAL"*/
    dImpLin         FORMAT "->>>>,>>9.9999" COLUMN-LABEL "TOTAL"
    x-percepcion    FORMAT "X"              COLUMN-LABEL " "
    WITH NO-BOX /*NO-LABELS NO-UNDERLINE*/ STREAM-IO DOWN WIDTH 164.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


&ANALYZE-SUSPEND _UIB-PREPROCESSOR-BLOCK 

/* ********************  Preprocessor Definitions  ******************** */

&Scoped-define PROCEDURE-TYPE Procedure
&Scoped-define DB-AWARE no



/* _UIB-PREPROCESSOR-BLOCK-END */
&ANALYZE-RESUME



/* *********************** Procedure Settings ************************ */

&ANALYZE-SUSPEND _PROCEDURE-SETTINGS
/* Settings for THIS-PROCEDURE
   Type: Procedure
   Allow: 
   Frames: 0
   Add Fields to: Neither
   Other Settings: CODE-ONLY COMPILE
 */
&ANALYZE-RESUME _END-PROCEDURE-SETTINGS

/* *************************  Create Window  ************************** */

&ANALYZE-SUSPEND _CREATE-WINDOW
/* DESIGN Window definition (used by the UIB) 
  CREATE WINDOW Procedure ASSIGN
         HEIGHT             = 4.88
         WIDTH              = 40.
/* END WINDOW DEFINITION */
                                                                        */
&ANALYZE-RESUME

&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _INCLUDED-LIB Procedure 
/* ************************* Included-Libraries *********************** */

{src/bin/_prns.i}

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


 


&ANALYZE-SUSPEND _UIB-CODE-BLOCK _CUSTOM _MAIN-BLOCK Procedure 


/* ***************************  Main Block  *************************** */
/* Definimos impresoras */

DEF VAR rpta AS LOG NO-UNDO.
SYSTEM-DIALOG PRINTER-SETUP UPDATE rpta.
IF rpta = NO THEN RETURN.
DEFINE VARIABLE X-ORDCOM AS CHARACTER FORMAT "X(18)".

IF FacCPedi.coddoc = "PED" THEN X-ORDCOM = "Orden de Compra : ".
ELSE X-ORDCOM = "Solicitud Cotiz.: ".
   
DEFINE FRAME F-FtrCot
    HEADER
    X-EnLetras   FORMAT "x(100)" SKIP 
    x-Percepcion SKIP
    "  DTO.GLOBAL :" AT 70 FacCPedi.ImpDto FORMAT '->>>,>>9.99' SKIP
    "NETO A PAGAR : "  AT 70  FORMAT "x(20)" 
    SUBSTRING(C-MONEDA,9,4)   FORMAT "X(10)" 
    STRING(F-ImpTot,">>,>>>,>>9.99")  FORMAT "x(20)" SKIP    
    "OBSERVACIONES : "  FORMAT "x(20)" SKIP 
    FacCPedi.Glosa VIEW-AS TEXT FORMAT "X(80)" SKIP
    "--------------------------------------------------" SKIP
    C-OBS[1] VIEW-AS TEXT FORMAT "X(315)"  SKIP 
    C-OBS[2] VIEW-AS TEXT FORMAT "X(315)"  SKIP 
    "--------------------------------------------------                            ------------------- " SKIP
    "                                                                              VoBo Jefe de Ventas " SKIP
    "HORA : " AT 1 STRING(TIME,"HH:MM:SS") "  " S-USER-ID SKIP  
    WITH PAGE-BOTTOM NO-LABELS NO-BOX STREAM-IO WIDTH 360.
    

DEFINE FRAME F-HdrPed
    HEADER 
    SKIP(4)
    "Page:" AT 110 PAGE-NUMBER FORMAT ">>9" SKIP
    S-NOMCIA FORMAT "X(45)" SKIP
    W-DIRALM  at 1 FORMAT "x(55)" 
    W-TLFALM  at 60 FORMAt "x(65)" SKIP
    C-TitDoc  AT 80 FORMAT "X(22)" 
    /*FacCPedi.NroPed AT 102 FORMAT "XXXXXX-XXXXXXXXXXXX" SKIP*/
    FacCPedi.NroPed AT 102 FORMAT "x(12)" SKIP
    "Señor(es) : " TO 25 C-Descli 
    "Direccion : " TO 25 FacCPedi.DirCli FORMAT "x(55)" "Emision         : " TO 100 FacCPedi.FchPed SKIP
    "R.U.C.    : " TO 25 gn-clie.Ruc                   "Vencimiento     : " TO 100 FacCPedi.fchven FORMAT "99/99/9999"  SKIP
    "<OFICINA> "   TO 6
    "Vendedor  : " TO 25 C-NomVen       X-ORDCOM TO 100 FacCPedi.ordcmp SKIP
    "Cond.Venta: " TO 25 C-NomCon       "Moneda          : " TO 100 C-Moneda        SKIP
/*     "------------------------------------------------------------------------------------------------------------------------------" SKIP  */
/*     "ITEM CODIGO   CANTIDAD UND.             D E S C R I P C I O N             M A R C A      PRECI_VTA     DSCTOS.     TOTAL NETO  " SKIP */
/*     "------------------------------------------------------------------------------------------------------------------------------" SKIP  */

    WITH PAGE-TOP NO-LABELS NO-BOX STREAM-IO WIDTH 164.
                         


/*OUTPUT  TO VALUE(s-port-name) PAGE-SIZE 48.*/
OUTPUT  TO PRINTER PAGE-SIZE 48.
PUT CONTROL {&PRN0} + {&PRN5A} + CHR(66) + {&PRN3}.     
              
DEF VAR x-PorDto AS DEC NO-UNDO.
I-NroItm = 0.
FOR EACH FacDPedi NO-LOCK WHERE FacDPedi.CodCia = FacCPedi.CodCia 
    AND FacDPedi.CodDoc = FacCPedi.CodDoc 
    AND FacDPedi.CodDiv = FacCPedi.CodDiv
    AND FacDPedi.NroPed = FacCPedi.NroPed, 
    FIRST almmmatg OF FacDPedi NO-LOCK
    BREAK BY FacDPedi.NroPed 
          BY FacDPedi.NroItm:
    I-NroItm = I-NroItm + 1.
    F-PreNet = FacDPedi.preuni * ( 1 - FacDPedi.PorDto / 100 ).
    VIEW  FRAME F-HdrPed.
    x-dscto = 0.

    /*Incluye o no IGV*/

    IF NOT l-incigv THEN 
        dImpLin = ROUND(FacDPedi.ImpLin / (1 + FacCPedi.PorIgv / 100),2). 
    ELSE dImpLin = FacDPedi.ImpLin.             
    dPreUni = ROUND(dImpLin / FacDPedi.CanPed,2).

    F-PesMat = F-PesMat + FacDPedi.PesMat.
    x-PorDto = ( 1 -  ( 1 - Facdpedi.Por_Dsctos[1] / 100 ) *
                ( 1 - Facdpedi.Por_Dsctos[2] / 100 ) *
                ( 1 - Facdpedi.Por_Dsctos[3] / 100 ) ) * 100.
    IF x-PorDto = 0 THEN f-PreUni = Facdpedi.PreUni.
    ELSE f-PreUni = Facdpedi.ImpLin / Facdpedi.CanPed.

    DISPLAY  
            I-NroItm  
            FacDPedi.codmat
            FacDPedi.CanPed
            FacDPedi.undvta
            (IF Facdpedi.Libre_c05 <> '' AND Facdpedi.Libre_c05 <> 'OF' THEN Facdpedi.Libre_c05 + ' - '  ELSE '') + Almmmatg.desmat @ almmmatg.desmat
            almmmatg.desmar
            Facdpedi.PreUni
            x-PorDto WHEN x-PorDto <> 0 @ FacDPedi.PorDto
            /*F-PreUni*/
            dPreUni
            /*F-ImpLin */
            dImpLin
            "*" WHEN Facdpedi.canapr > 0 @ x-percepcion  
            WITH FRAME F-DetaCot.  
    IF LAST-OF(FacDPedi.NroPed) THEN 
        PUT "PESO: " F-PesMat FORMAT '>>>,>>9.99' SKIP.
    
END.
VIEW  FRAME F-FtrCot.
OUTPUT CLOSE.

/* _UIB-CODE-BLOCK-END */
&ANALYZE-RESUME


